;;; almost-mono-themes.el --- Almost monochromatic color themes -*- lexical-binding: t; -*-

;; Copyright (C) 2019, 2020 John Olsson

;; Author: John Olsson <john@cryon.se>
;; Maintainer: John Olsson <john@cryon.se>
;; URL: https://github.com/cryon/almost-mono-themes
;; Created: 9th May 2019
;; Version: 1.0.0
;; Keywords: faces
;; Package-Requires: ((emacs "24"))

;; This file is free software: you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published
;; by the Free Software Foundation, either version 3 of the License,
;; or (at your option) any later version.

;; This file is distributed in the hope that it will be useful, but
;; WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
;; General Public License for more details.

;; You should have received a copy of the GNU General Public License
;; along with this file.  If not, see <http://www.gnu.org/licenses/>.

;;; Commentary:

;; A suite of almost monochrome Emacs themes

;;; Code:

(defconst almost-mono-themes-colors
  '((white . ((background . "#ffffff")
          (foreground . "#000000")
          (weak       . "dim gray")
          (weaker     . "gray77")
          (weakest    . "gray94")
          (highlight  . "#fda50f")
          (warning    . "#ff0000")
          (success    . "#00ff00")
          (string     . "SpringGreen4")))

    (black . ((background . "#000000")
          (foreground . "#ffffff")
          (weak       . "#aaaaaa")
          (weaker     . "#666666")
          (weakest    . "#222222")
          (highlight  . "#fda50f")
          (warning    . "#ff0000")
          (success    . "#00ff00")
          (string     . "#a7bca4")))

    (gray .  ((background . "#2b2b2b")
          (foreground . "#ffffff")
          (weak       . "#aaaaaa")
          (weaker     . "#666666")
          (weakest    . "#222222")
          (highlight  . "#fda50f")
          (warning    . "#ff0000")
          (success    . "#00ff00")
          (string     . "#a7bca4")))

    (cream . ((background . "#f0e5da")
          (foreground . "#000000")
          (weak       . "#7d7165")
          (weaker     . "#c4baaf")
          (weakest    . "#dbd0c5")
          (highlight  . "#fda50f")
          (warning    . "#ff0000")
          (success    . "#00ff00")
          (string     . "#3c5e2b")))))

(defmacro almost-mono-themes--variant-with-colors (variant &rest body)
  "Execute BODY in a scope where the different colors for given VARIANT is bound."
  `(let* ((colors (or (cdr (assoc ,variant almost-mono-themes-colors))
              (error "No such theme variant")))
      (background (cdr (assoc 'background colors)))
      (foreground (cdr (assoc 'foreground colors)))
      (weak           (cdr (assoc 'weak colors)))
      (weaker     (cdr (assoc 'weaker colors)))
      (weakest    (cdr (assoc 'weakest colors)))
      (highlight  (cdr (assoc 'highlight colors)))
      (warning    (cdr (assoc 'warning colors)))
      (success    (cdr (assoc 'success colors)))
      (string     (cdr (assoc 'string colors))))
     ,@body))

(defmacro almost-mono-themes--faces-spec ()
  "Provide the faces specification."
  (quote
   (mapcar
    (lambda (entry) (list (car entry) `((t ,@(cdr entry)))))
    `(

      ;; default
      (default (:background ,background :foreground ,foreground))
      (fringe  (:background ,background))
      (region  (:background ,highlight  :foreground ,foreground))
      (show-paren-match (:background ,background :foreground ,success :bold t))
      (show-paren-mismatch (:background ,background :foreground ,warning :bold t))
      (minibuffer-prompt (:weight bold :foreground ,foreground))
      (isearch (:background ,weakest :foreground ,foreground :bold t))
      (lazy-highlight (:background ,weakest :foreground ,foreground))
      (link (:underline t))

      ;; mode line
      (mode-line (:box (:line-width -1 :color ,weaker)
               :background ,weakest :foreground ,foreground))

      (mode-line-inactive (:box (:line-width -1 :color ,weaker)
                :background ,background :foreground ,weaker))

      ;; font lock
      (font-lock-keyword-face (:bold t ))
      (font-lock-function-name-face (:foreground ,string :bold t))
      (font-lock-variable-name-face (:foreground ,foreground))
      (font-lock-warning-face (:foreground ,foreground :underline (:color ,warning :style wave)))
      (font-lock-builtin-face (:bold t))
      (font-lock-variable-name-face (:foreground ,string :italic t :family "Monospace"))
      (font-lock-constant-face (:bold t :family "Monospace"))
      (font-lock-type-face (:underline t))
      (font-lock-preprocessor-face (:foreground ,string :italic t :font "Monospace"))
      (font-lock-comment-face (:foreground ,weak :italic t :family "Monospace"))
      (font-lock-string-face (:foreground ,string))
      (font-lock-doc-face (:inherit font-lock-comment-face))
      (line-number (:foreground ,weaker))
      (linum (:inherit line-number))
      (vertical-border (:foreground ,weaker))

      ;; eshell
      (eshell-prompt (:foreground ,foreground :bold t))
      (eshell-ls-directory (:foreground ,foreground :bold t))
      (eshell-ls-archive (:inherit eshell-ls-unreadable))
      (eshell-ls-backup (:inherit eshell-ls-unreadable))
      (eshell-ls-clutter (:inherit eshell-ls-unreadable))
      (eshell-ls-executable (:inherit eshell-ls-unreadable))
      (eshell-ls-missing (:inherit eshell-ls-unreadable))
      (eshell-ls-product (:inherit eshell-ls-unreadable))
      (eshell-ls-readonly (:inherit eshell-ls-unreadable))
      (eshell-ls-special (:inherit eshell-ls-unreadable))
      (eshell-ls-symlink (:inherit eshell-ls-unreadable))

      ;; company mode
      (company-tooltip (:background ,weakest :foreground ,foreground))
      (company-tooltip-selection (:background ,weaker :foreground ,foreground))
      ;;(company-tooltip-search (:background "#ff0000" :foreground "#00ff00"))
      (company-tooltip-common (:bold t))
      (company-tooltip-common-selection (:bold t))
      (company-scrollbar-bg (:background ,weaker))
      (company-scrollbar-fg (:background ,weak))
      (company-tooltip-annotation-selection (:background ,weaker :foreground ,foreground :italic t :family "Monospace"))
      (company-tooltip-annotation (:background ,weakest :foreground ,weak :italic t :family "Monospace"))

      ;; consult
      (consult-preview-line (:background ,weakest))
      (consult-line-number-prefix (:foreground ,string))
      (consult-preview-cursor (:foreground ,string))

      ;; git gutter
      (git-gutter:modified (:background ,highlight :foreground ,highlight))
      (git-gutter:added (:background ,success :foreground ,success))
      (git-gutter:deleted (:background ,warning :foreground ,warning))

      ;; diff hl
      (diff-hl-change (:background ,highlight :foreground ,highlight))
      (diff-hl-insert (:background ,success :foreground ,success))
      (diff-hl-delete (:background ,warning :foreground ,warning))

      ;; hl line
      (hl-line (:background ,weakest))
      (highlight-current-line-face (:inherit hl-line))

      ;; orderless
      (orderless-match-face-0 (:bold t :underline t))
      (orderless-match-face-1 (:bold t :underline t))
      (orderless-match-face-2 (:bold t :underline t))
      (orderless-match-face-3 (:bold t :underline t))
      
      ;; selectrum
      (selectrum-current-candidate (:background ,weakest))
      (selectrum-group-title (:foreground ,string))

      ;; org mode
      (org-table (:foreground ,weak))
      (org-drawer (:foreground ,string))
      (org-level-1 (:bold t))
      (org-level-2 (:foreground ,foreground :bold t))
      (org-level-3 (:inherit org-level-2))
      (org-level-4 (:inherit org-level-2))
      (org-level-5 (:inherit org-level-2))
      (org-headline-done (:foreground ,weak :strike-through t))
      (org-headline-todo (:foreground ,foreground))
      (org-ellipsis (:foreground ,weak))
      (org-formula (:inherit org-ellipsis))
      (org-done (:foreground ,string))
      (org-todo (:inherit org-done))
      (org-property-value (:foreground ,weak))
      (org-link (:foreground ,string :underline t))
      (org-date (:foreground ,weak :underline t))
      (org-agenda-current-time (:bold t))
      (org-agenda-date (:foreground ,foreground :bold t))
      (org-agenda-date-weekend (:inherit org-agenda-date))
      (org-agenda-date-today (:foreground ,string :bold t))
      (org-agenda-structure (:inherit org-level-1))
      (org-agenda-structure-secondary (:inherit org-agenda-structure))
      (org-agenda-date-weekend-today (:inherit org-agenda-date-today))
      (org-time-grid (:inherit org-property-value))
      (org-imminent-deadline (:foreground ,string :bold t))
      (org-warning (:bold t))
      (org-scheduled (:foreground ,foreground))
      (org-scheduled-today (:inherit org-scheduled))
      (org-scheduled-previously (:inherit org-scheduled))
      (org-agenda-done (:foreground ,weak :strike-through t))
      (org-upcoming-deadline (:foreground ,string))

      ;; whitespace mode
      (whitespace-empty (:foreground ,weaker :background ,background))
      (whitespace-big-indent (:inherit whitespace-empty))
      (whitespace-space (:inherit whitespace-empty))
      (whitespace-indentation (:inherit whitespace-empty))
      (whitespace-line (:foreground ,weak))
      (whitespace-space-before-tab (:foreground ,string :underline t))
      (whitespace-trailing (:inherit whitespace-space-before-tab))
      ))))


(defun almost-mono-themes--variant-name (variant)
  "Create symbol for color theme variant VARIANT."
  (intern (format "almost-mono-%s" (symbol-name variant))))

(defmacro almost-mono-themes--define-theme (variant)
  "Define a theme for the almost-mono variant VARIANT."
  (let ((name (almost-mono-themes--variant-name variant))
        (doc (format "almost mono theme (%s version)" variant)))
    `(progn
       (deftheme ,name ,doc)
       (put ',name 'theme-immediate t)
       (almost-mono-themes--variant-with-colors
        ',variant
        (apply 'custom-theme-set-faces ',name
               (almost-mono-themes--faces-spec)))
       (provide-theme ',name))))

;;;###autoload
(when (and (boundp 'custom-theme-load-path) load-file-name)
  (add-to-list 'custom-theme-load-path
	       (file-name-as-directory (file-name-directory load-file-name))))

(provide 'almost-mono-themes)

;;; almost-mono-themes.el ends here
